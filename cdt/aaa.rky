#lang racket

(define (extend ρ n v)
  (cons (cons n v) ρ))

(struct go
  (result)
  #:transparent)
(struct stop
  (expr message)
  #:transparent)

(require (for-syntax syntax/parse))

(struct Closure
  (env var body)
  #:transparent)

(define-syntax (go-on stx)
  (syntax-parse stx
    [(go-on () result)
     (syntax/loc stx
       result)]
    [(go-on ([pat0 e0] [pat e] ...) result)
     (syntax/loc stx
       (match e0
         [(go pat0) (go-on ([pat e] ...) result)]
         [(go v)    (error 'go-on "Pattern did not mathc ~v" v)]
         [(stop expr msg) (stop expr msg)]))]))

(define (type=? t1 t2)
  (match* (t1 t2)
    [('Nat 'Nat)
     #t]
    [(`(→ ,A1 ,R1) `(→ ,A2 ,R2))
     (and (type=? A1 A2) (type=? R1 R2))]
    [(_ _)
     #f]))

(define (type? t)
  (type=? t t))

(define (synth Γ expr)
  (match expr
    ; Annotations are an explicit form of type checking
    [`(the ,t ,e)
     (if (not (type? t))
         (stop expr (format "~a is not a type" t))
         (go-on ([_ (check Γ e t)])
                (go t)))]
    [`(rec ,t ,target ,base ,step)
     (go-on ([target-type (synth Γ target)]
             [_           (if (type=? target-type 'Nat)
                              (go 'ok)
                              (stop target (format "Expected Nat but got ~v"
                                                   target-type)))]
             [_           (check Γ base t)]
             [_           (check Γ step `(→ Nat (→ ,t ,t)))])
            (go t))]
    [x #:when (and (symbol? x)
                   (not (memv x '(the rec λ zero add1))))
       (match (assv x Γ)
         [#f         (stop x "Variable not found")]
         [(cons _ t) (go t)])]
    (`(,rator ,rand)
     (go-on ([rator-type (synth Γ rator)])
            (match rator-type
              [`(→ ,rator-arg-type, rator-ret-type)
               (go-on ([_ (check Γ rand rator-arg-type)])
                      (go rator-ret-type))]
              [else (stop rator (format "~v is not a function type"
                                        rator-type))])))))

(define (check Γ expr t)
  (match expr
    ['zero
     (if (type=? t 'Nat)
         (go 'ok)
         (stop expr (format "zero is not a ~v" t)))]
    [`(add1 ,n)
     (if (type=? t 'Nat)
         (go-on ([_ (check Γ n 'Nat)])
                (go 'ok))
         (stop expr (format "add1 is not a contructor for ~v" t)))]
    [`(λ (,arg) ,body)
     (match t
       [`(→ ,arg-type ,ret-type)
        (go-on ([_ (check (
                           extend Γ arg arg-type) body ret-type)])
               (go 'ok))]
       [x
        (stop expr (format "Expected an → but found ~a" x))])]
    [x
     (go-on ([x-type (synth Γ expr)])
            (if (type=? t x-type)
                (go 'ok)
                (stop expr
                      (format "Infered type ~v but expected ~v"
                              x-type x))))]))

(define (check-program Γ prog)
  (match prog
    ['()
     (go Γ)]
    [(cons `(define ,name ,expr) rest)
     (go-on ([t (synth Γ expr)])
            (check-program (extend Γ name t) rest))]
    [(cons expr rest)
     (go-on ([t (synth Γ expr)])
            (begin
              (printf "~a has type ~a\n" expr t)
              (check-program Γ rest)))]))

(struct Zero ()
  #:transparent)
(struct Add1 (predecessor)
  #:transparent)
(struct Neutral (type neutral-expr)
  #:transparent)
(struct Neutral-Recursion (type target base step)
  #:transparent)
(struct The (type value)
  #:transparent)
(struct Neutral-Variable
  (name))
(struct Neutral-Application
  (rator rand))

(define (val ρ e)
  (match e
    [`(the ,type ,expr)
     (val ρ expr)]
    ['zero (Zero)]
    [`(add1 ,n) (Add1 (val ρ n))]
    [x #:when (and (symbol? x)
                   (not (memv x '(the zero add1 λ rec))))
     (cdr (assv x ρ))]
    [`(λ (,x) ,b)
     (Closure ρ x b)]
    [`(rec ,type ,target ,base ,step)
     (do-rec type (val ρ target) (val ρ base) (val ρ step))]
    [`(,rator ,rand)
     (do-ap (val ρ rator) (val ρ rand))]))

(define (do-ap fun arg)
  (match fun
    [(Closure ρ x e)
     (val (extend ρ x arg) e)]
    [(Neutral `(→ ,from ,to) expr)
     (Neutral to (Neutral-Application expr (The from arg)))]))

(define (do-rec type target base step)
  (match target
    [(Zero) base]
    [(Add1 n)
     (do-ap (do-ap step n)
            (do-rec type target step))]
    [(Neutral 'Nat ne)
     (Neutral type
              (Neutral-Recursion type
                                 ne
                                 (The type base)
                                 (The `(→ Nat (→ ,type ,type)) step)))]))